apply plugin: 'com.android.application'
apply from: "${project.code_quality_rootDir}/config/quality/quality.android.gradle"

def keystorePropsFile = rootProject.file("keystore.properties")
def keystoreProps = new Properties()
keystoreProps.load(new FileInputStream(keystorePropsFile))

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "io.szugyi.nytimes"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['resourcePackageName': android.defaultConfig.applicationId,
                             'classSuffix'        : "Impl"]
            }
        }
    }

    signingConfigs {
        signingKey {
            storeFile file(keystoreProps['storeFile'])
            storePassword keystoreProps['storePassword']
            keyAlias keystoreProps['keyAlias']
            keyPassword keystoreProps['keyPassword']
        }
    }

    flavorDimensions "default"

    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix ".mock"
        }
        integration {
            dimension "default"
            applicationIdSuffix ".integration"

            buildConfigField "String", "BASE_URL", '"http://api.nytimes.com/"'
            buildConfigField "String", "API_KEY", '"f6c453218efe4f3e8750e53815e93892"'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.signingKey
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signingKey
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE'
    }
}

ext {
    androidAnnotationsVersion = "4.4.0"
    daggerVersion = '2.13'
    jacksonVersion = '2.8.8'
    okHttpVersion = '3.8.0'
    retrofitVersion = '2.3.0'
    stethoVersion = '1.5.0'
    supportLibVersion = '26.1.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.squareup.picasso:picasso:2.5.2'

    annotationProcessor "org.androidannotations:androidannotations:$androidAnnotationsVersion"
    implementation "org.androidannotations:androidannotations-api:$androidAnnotationsVersion"

    integrationImplementation "com.facebook.stetho:stetho:$stethoVersion"
    integrationImplementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"
    integrationImplementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    integrationImplementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    integrationImplementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    integrationImplementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    integrationImplementation "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
    integrationImplementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    integrationImplementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    integrationImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    provided 'org.glassfish:javax.annotation:10.0-b28'
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
}
