apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

checkstyle {
    toolVersion = "8.1"
}

pmd {
    toolVersion = '5.8.1'
}

findbugs {
    toolVersion = '3.0.1'
}

tasks.withType(Checkstyle) {
    ignoreFailures = false

    configFile file("${project.code_quality_rootDir}/config/quality/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.code_quality_rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath

    source 'src/main/java'
    source 'src/test/java'

    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        xml.destination = file("${project.buildDir}/outputs/checkstyle/checkstyle.xml")

        html.enabled = false
        html.destination = file("${project.buildDir}/outputs/checkstyle/checkstyle.html")
    }

    classpath = files()
}

tasks.withType(FindBugs) {
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    classes = files("${project.buildDir}/classes/")
    excludeFilter = new File("${project.code_quality_rootDir}/config/quality/findbugs/findbugs-filter.xml")

    source 'src/main/java'
    source 'src/test/java'

    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        xml.destination = file("${project.buildDir}/outputs/findbugs/findbugs.xml")

        html.enabled = false
        html.destination = file("${project.buildDir}/outputs/findbugs/findbugs.html")
    }

    classpath = files()
}

tasks.withType(Pmd) {
    ignoreFailures = false
    ruleSetFiles = files("${project.code_quality_rootDir}/config/quality/pmd/pmd-ruleset.xml")

    source 'src/main/java'
    source 'src/test/java'

    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        xml.destination = file("${project.buildDir}/outputs/pmd/pmd.xml")

        html.enabled = false
        html.destination = file("${project.buildDir}/outputs/pmd/pmd.html")
    }
}
